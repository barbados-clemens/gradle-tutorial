plugins {
    id "dev.nx.gradle.project-graph" version "0.1.0"
	id 'org.springframework.boot' version '3.2.2'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'java'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation project(':library')
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

// Replace the default test task with an error
test {
	doFirst {
		throw new GradleException("The 'test' task has been replaced with 'apiTest'. Please use './gradlew apiTest' instead.")
	}
}

// Create apiTest as a replacement for test
tasks.register('apiTest', Test) {
	useJUnitPlatform()
	group = 'verification'
	description = 'Runs the API tests'
	testClassesDirs = sourceSets.test.output.classesDirs
	classpath = sourceSets.test.runtimeClasspath
}

// Make check depend on apiTest instead of test
check {
	dependsOn apiTest
}

allprojects {
    apply {
        plugin("dev.nx.gradle.project-graph")
    }
}